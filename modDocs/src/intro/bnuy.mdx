---
title: 'Example Mod'
desc: 'In-depth breakdown of `sniper_bnuy.ini`, the example mod.'
order: 2
---

# What

For more context, I recommend reading the [Quickstart](/rm-docs/intro/quickstart) article first. We're going to take a more in-depth look at `sniper_bnuy.ini`, Rusted Moss's example mod.

# `sniper_bnuy.ini`

```mini
[object_list]
controller = enabled
instance = enabled

[controller_events]
room_start = "i = 0; while ( i < instance_number(oenemy_flame_sniper) ) { c = instance_find(oenemy_flame_sniper,i); instance_create_depth(c.x,c.y,30,omod_instance); i += 1; }"

[instance_events]
create = "self.parent = instance_nearest(self.x,self.y,oenemy_flame_sniper);"
draw = "if ( instance_exists(self.parent)) { draw_sprite_ext( spuck_bunny_ears, 0, self.parent.x, self.parent.y-8, 1, 1, 0, c_white, 1 ) }"
step = "if ( instance_exists(self.parent) ) { if ( self.parent.legs == smaya_legs_run )  { self.parent.vsp = self.parent.vsp -4; } }"
```

This a mod file, and contains the Catspeak code that runs. Let's break it down.

# Object List

```mini
[object_list]
controller = enabled
instance = enabled
```

This section tells Rusted Moss what [Game Objects](/rm-docs/gml/objects) to run code for. In this case, `controller` and `instance` Game Objects are enabled.

# Object Events

```mini
[controller_events]
room_start = "i = 0; while ( i < instance_number(oenemy_flame_sniper) ) { c = instance_find(oenemy_flame_sniper,i); instance_create_depth(c.x,c.y,30,omod_instance); i += 1; }"
```

This is where we define the Catspeak code that runs on each [Event](/rm-docs/gml/events). The header (`[controller_events]`) tells us what Game Object to run code for, and the key (`room_start`) tells us the Event. Inside the `"` quotes, as the value for `room_start`, is our Catspeak code. It's not very readable, so I'll add some line breaks for now and friendly comments.

```sp
i = 0
-- For each `oenemy_flame_sniper` in the current room ...
while ( i < instance_number(oenemy_flame_sniper) ) {
  -- ... find that Instance ...
  c = instance_find(oenemy_flame_sniper, i);
  -- ... and make a new `omod_instance` Instance at its position.
  instance_create_depth(c.x, c.y, 30, omod_instance);
  i += 1;
}
```

Pretty simple, we're making an `omod_instance` Instance for each `oenemy_flame_sniper` in the [Room](/rm-docs/gml/architecture#rooms), when we first enter a Room. There's a few other notes for this code.

1. _Line Breaks_: Because of [boring technical reasons](/rm-docs/modding/rmml#coding-in-one-line), we cannot add line breaks to our Catspeak source code. There are [ways around it](/rm-docs/mose/mods#globalmod_map), but for now we'll need to remove all of the line breaks. For clarity, I'll be including line breaks in code examples.
1. _Gamemaker Functions_: [`instance_number`](https://manual.gamemaker.io/lts/en/GameMaker_Language/GML_Reference/Asset_Management/Instances/instance_number.htm), [`instance_find`](https://manual.gamemaker.io/lts/en/GameMaker_Language/GML_Reference/Asset_Management/Instances/instance_find.htm), and [`instance_create_depth`](https://manual.gamemaker.io/lts/en/GameMaker_Language/GML_Reference/Asset_Management/Instances/instance_create_depth.htm) are all Gamemaker functions. Rusted Moss conveniently exposes _every_ Gamemaker function for us, which is cool. However, because of [boring technical reasons](/rm-docs/modding/limitations#lts-bugs), some of these functions just don't work.
1. _`oenemy_flame_sniper`_: This is one of Rusted Moss's Game Objects, specifically the enemies found in certain Living Quarters rooms. Similar to Gamemaker functions, Rusted Moss exposes _every_ Game Object as well. [Undertale Mod Tool](/rm-docs/modding/utmt) lists every Game Object for our convenience.
1. _`room_start`_: This Event runs when we first enter a Room. A single `omod_controller` Instance is created for us when the game starts, so we don't need to do anything else for this event to run.
1. _`omod_instance`_: This is a special Game Object, since we get to write code for it! Let's see what it's code looks like:

# `omod_instance` Code

```mini
[instance_events]
create = "self.parent = instance_nearest(self.x,self.y,oenemy_flame_sniper);"
draw = "if ( instance_exists(self.parent)) { draw_sprite_ext( spuck_bunny_ears, 0, self.parent.x, self.parent.y-8, 1, 1, 0, c_white, 1 ) }"
step = "if ( instance_exists(self.parent) ) { if ( self.parent.legs == smaya_legs_run )  { self.parent.vsp = self.parent.vsp -4; } }"
```

Since the game makes an `omod_controller` for us, we don't need to create a controller Instance. However, `instance` Game Objects must be created by our code (in this example, the `create` event creates our Instances). There are also three events this time, so let's break it down.

```sp
-- Create event, add an Instance variable for the closest `oenemy_flame_sniper`
self.parent = instance_nearest(self.x, self.y, oenemy_flame_sniper);
```

```sp
-- Draw event, draw bunny ears on the head of each "parent" Instance
if ( instance_exists(self.parent)) {
  draw_sprite_ext(
    spuck_bunny_ears, 0,
    self.parent.x, self.parent.y - 8,
    1, 1,
    0, c_white, 1
  )
}
```

```sp
-- Step event, if the parent is "running", make it "jump"
if ( instance_exists(self.parent) ) {
  if ( self.parent.legs == smaya_legs_run ) {
    self.parent.vsp = self.parent.vsp - 4;
  }
}
```

Each `omod_instance` Instance is responsible for managing a single `oenemy_flame_sniper` Instance, drawing its ears and jumping. There are a few things to note here.

1. _`self.parent`_: This is an Instance variable on `self`, which is the currently scoped Instance. In most cases, this will be the moddable Instance you'e writing code for, however you can use the [`with` statement](https://manual.gamemaker.io/lts/en/GameMaker_Language/GML_Overview/Language_Features/with.htm) to change the current scope.
1. _`legs` and `vsp`_: These are custom Instance variables on `oenemy_flame_sniper` Instances. To find out how what they do, you'll want to investigate the [source code](/rm-docs/modding/utmt#where-the-code-at) using Undertale Mod Tool.
1. _`spuck_bunny_ears`_: This is a [Sprite](/rm-docs/gml/architecture#sprites), _all_ of which are exposed for modding. Because of [boring technical reasons](/rm-docs/modding/limitations#sprites), our ability to modify Sprites is slightly limited, but there are hundreds of existing Sprites we can use.
1. _`create`, `draw`, and `step` Events_: Unlike `controller`, `instance` Game Object need to be created first (since there is no Instance for the Event to run on). `create` runs immediately after creation (halting execution). `draw` and `step` both happen each frame, [with `draw` triggering after `step`](/rm-docs/gml/events#execution-order).
